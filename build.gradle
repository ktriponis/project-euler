plugins {
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.1'
        testImplementation 'org.assertj:assertj-core:3.12.2'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.1'
    }

    test {
        useJUnitPlatform()
    }

    task printResults(type: Test, group: 'verification') {
        useJUnitPlatform {
            includeTags 'result'
        }
        outputs.upToDateWhen { false }
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
        }
    }
}

task jacocoRootReport(type: JacocoReport, group: 'verification') {
    additionalSourceDirs.from = subprojects.sourceSets.main.allSource.srcDirs
    sourceDirectories.from = subprojects.sourceSets.main.allSource.srcDirs
    classDirectories.from = subprojects.sourceSets.main.output
    executionData.from = subprojects.jacocoTestReport.executionData

    reports {
        xml.enabled = true
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}
